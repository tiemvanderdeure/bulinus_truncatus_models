model{
    
    ## Weekly egg laying model Priors
    q_mu ~ dunif(0.1, 0.5)
    T0_mu ~ dunif(9, 15)
    Tm_mu ~ dunif(30, 38)
    q_sigma ~ dunif(0,0.3)
    T0_sigma ~ dunif(0,5)
    Tm_sigma ~ dunif(0,5)
    q_tau <- 1 / (q_sigma * q_sigma)
    T0_tau <- 1 / (T0_sigma * T0_sigma)
    Tm_tau <- 1 / (Tm_sigma * Tm_sigma)

    # Clutch size model priors
    cs_mu ~ dnorm(5, 2)
    cs_sigma ~ dunif(0, 2)
    cs_tau <- 1 / (cs_sigma * cs_sigma)

    # Priors for variance between groups within population
    group_sigma ~ dunif(0, 10)
    group_tau <- 1 / (group_sigma * group_sigma)

    # Priors for individual snails (higher lambda is lower variance)
    lambda_neggs ~ dunif(0, 10)
    lambda_cs ~ dunif(0, 50)

    ## Loop through observations of number of egg masses (multiple weeks per snail)
    for(i in 1:N.records){
        n_clutches[i] ~ dpois(n_clutches_snail[snail_id[i]])
    }

    ## Loop through observations of number of clutch sizes (multiple clutches per snail)
    for(i in 1:N.obs_cs){
        clutch_size[i] ~ dpois(clutch_size_snail[snail_id_cs[i]])
    }
    
    ## Loop through individuals
    for(i in 1:N.snails){
        n_clutches_snail[i] <- n_eggs_snail[i] / clutch_size_snail[i] # average number of clutches per week

        clutch_size_snail[i] ~ dgamma(clutch_size_exp[exp_id[i]]*lambda_cs, lambda_cs) T(1,)

        n_eggs_snail[i] ~ dgamma(r_neggs[i], lambda_neggs)
        r_neggs[i] <- max(n_eggs_exp[exp_id[i]]*lambda_neggs, 0.05) # need to set a lower bound to prevent an error
    }

    ## Loop through temperature for each population
    for(i in 1:N.experiments){
        # Calculate average number of eggs from Briere function
        n_eggs_exp[i] <- n_eggs_quad[i] * within_temp_range[i] + min_eggs[i] * (1 - within_temp_range[i])
        n_eggs_quad[i] ~ dnorm(q[pop_id[i]] * (temp[i] - T0[pop_id[i]]) * (Tm[pop_id[i]] - temp[i]), group_tau)
        within_temp_range[i] <- (Tm[pop_id[i]] > temp[i]) * (T0[pop_id[i]] < temp[i])
        min_eggs[i] ~ dunif(0.03, 1) # lower bound when outside of temp range

        # Estimate average clutch size
	    clutch_size_exp[i] ~ dnorm(cs_mu, cs_tau) T(1,)
    }

    ## Loop through populations
    for(i in 1:N.populations){
        # number of eggs parameters
        q[i] ~ dnorm(q_mu, q_tau) T(0,)
        T0[i] ~ dnorm(T0_mu, T0_tau)
        Tm[i] ~ dnorm(Tm_mu, Tm_tau)
    }
}
